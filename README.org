#+TITLE:DCC: D's Cruft Collection

This is where I dump opinionated snippets from my Emacs configuration
and related hacks, together with some of the thinking processes behind
them.

** Elisp Conventions
I generally prefix all of my functions and variables with the prefix
~my-~.  That's about it.

* My own ~org-superstar~ configuration
My own config is (maybe unsurprisingly) quite close to the default.
Apart from the obvious ~add-hook~ it boils down to this:
#+begin_src elisp
  (with-eval-after-load 'org-superstar
    (set-face-attribute 'org-superstar-item nil :height 1.2)
    (set-face-attribute 'org-superstar-header-bullet nil :height 1.2)
    (set-face-attribute 'org-superstar-leading nil :height 1.3))
  (setq org-superstar-headline-bullets-list
        '("‚óâ" ("üûõ" ?‚óà) "‚óã" "‚ñ∑"))
  (setq org-superstar-cycle-headline-bullets nil)
  (setq org-superstar-leading-fallback ?\s)
#+end_src
I am genuinely unsure whether I should change a better default for
~org-superstar-leading-fallback~.  I think either =?\s= or =.= (=FULL STOP=)
would probably be better for the majority of people.  I probably have
to reach out to the community.
* Greek Character Input with =C-x 8 g=
I must say, one of the things I have come to truly appreciate is the
concept of a compose key.  You have a rare character you wanna type,
you smash two vaguely related characters together, and the result is
what you wanted.  Take √ß (also known as =LATIN SMALL LETTER C CEDILLA=)
for example.  =Compose + , + c= = =√ß=.  Brilliant.  Emacs has something
similar, the =C-x 8= prefix.  It either takes multiple characters and
mashes them together, like =C-x 8 " A= = =√Ñ=, or takes a single character
as if to say "give me that one but weird" (see =C-x 8 P= = =¬∂=).  Another
thing both features have in common is that they both have a lot of
untapped potential.  All that room for cryptic mnemonics that possibly
make only sense to you!

So, how does =C-x 8= work?  It does not have a map like =C-x 4='s
~ctl-x-4-map~, and its entries are listed as "Key translations", not
"Global Bindings".  Well, it's in a special map, ~key-translation-map~.
Adding bindings to this map which are just single-char keyboard macros
also has the advantage of them showing up /as the actual characters/ in
help menus such as that of ~describe-bindings~, instead of just
unhelpfully reading =Keyboard Macro=.  Now to the actual code: I decided
to use the prefix =C-x 8 g= + whatever +letter makes sense in my head+
AUCTeX picked.  To be fair, I find most of the AUCTeX bindings
intuitive, which is why I decided to go for them after doing some
=.emacs= housekeeping.
#+begin_src elisp
  (defvar my-greek-prefix "C-x 8 g"
    "Keyboard prefix used for greek letters, as a string.")

  (defvar my-greek-alist nil
    "Alist of keys to be translated into greek characters.
  Each entry should take the form
  \(KEY . VALUE)

  where KEY is a string and VALUE a char.

  KEY should correspond to a string of keys which (prefixed by
  ‚Äòmy-greek-prefix‚Äô) translates into the corresponding VALUE.")

  (setq-default my-greek-alist ;; Rip off AUCTeX.
                '(("a" . ?Œ±)
                  ("b" . ?Œ≤)
                  ("c" . ?œá) ;; This one is now free!
                  ("d" . ?Œ¥)
                  ("D" . ?Œî)
                  ("e" . ?Œµ)
                  ("f" . ?œï)
                  ("v f" . ?œÜ)
                  ("F" . ?Œ¶)
                  ("g" . ?Œ≥)
                  ("G" . ?Œì)
                  ("h" . ?Œ∑)
                  ("i" . ?Œπ) ;; Makes the most sense here.
                  ("j" . ?Œ∏)
                  ("J" . ?Œò)
                  ("k" . ?Œ∫)
                  ("l" . ?Œª)
                  ("L" . ?Œõ)
                  ("m" . ?Œº)
                  ("n" . ?ŒΩ)
                  ("p" . ?œÄ)
                  ("P" . ?Œ†)
                  ("q" . ?œá)
                  ("r" . ?œÅ)
                  ("s" . ?œÉ)
                  ("S" . ?Œ£)
                  ("t" . ?œÑ)
                  ("u" . ?œÖ)
                  ("y" . ?œà)
                  ("Y" . ?Œ®)
                  ("x" . ?Œæ)
                  ("X" . ?Œû)
                  ("w" . ?œâ)
                  ("W" . ?Œ©)
                  ("z" . ?Œ∂)))

  (dolist (entry my-greek-alist)
    (define-key key-translation-map
      (kbd (format "%s %s" my-greek-prefix (car entry)))
      (char-to-string (cdr entry))))
#+end_src

#  LocalWords:  Elisp √ß AUCTeX config
